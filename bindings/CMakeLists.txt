#
# Copyright (c) 2023 INRIA
# Author: Louis Montaut
#

include(${PROJECT_SOURCE_DIR}/cmake/stubs.cmake)

ADD_CUSTOM_TARGET(python)
SET_TARGET_PROPERTIES(python PROPERTIES EXCLUDE_FROM_DEFAULT_BUILD True)

# Name of the Python library
SET(LIBRARY_NAME pycolbench)

SET(${LIBRARY_NAME}_HEADERS
  colbench.hh
  pickle.hh
)

SET(${LIBRARY_NAME}_SOURCES
  colbench.cc
)

ADD_LIBRARY(${LIBRARY_NAME} SHARED ${${LIBRARY_NAME}_SOURCES} ${${LIBRARY_NAME}_HEADERS})
TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} SYSTEM PRIVATE ${PYTHON_INCLUDE_DIRS})

ADD_DEPENDENCIES(python ${LIBRARY_NAME})
ADD_HEADER_GROUP(${LIBRARY_NAME}_HEADERS)
ADD_SOURCE_GROUP(${LIBRARY_NAME}_SOURCES)

TARGET_LINK_BOOST_PYTHON(${LIBRARY_NAME} PUBLIC)
TARGET_LINK_LIBRARIES(${LIBRARY_NAME} PUBLIC
  ${PROJECT_NAME}
  eigenpy::eigenpy
  Boost::system)

SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES
  PREFIX ""
  SUFFIX "${PYTHON_EXT_SUFFIX}"
  LIBRARY_OUTPUT_NAME ${LIBRARY_NAME}
  LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bindings/${LIBRARY_NAME}"
  )

IF(UNIX AND NOT APPLE)
  SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES INSTALL_RPATH "\$ORIGIN/../../..")
ENDIF()

IF(IS_ABSOLUTE ${PYTHON_SITELIB})
  SET(ABSOLUTE_PYTHON_SITELIB ${PYTHON_SITELIB})
ELSE()
  SET(ABSOLUTE_PYTHON_SITELIB ${CMAKE_INSTALL_PREFIX}/${PYTHON_SITELIB})
ENDIF()
SET(${LIBRARY_NAME}_INSTALL_DIR ${ABSOLUTE_PYTHON_SITELIB}/${LIBRARY_NAME})

INSTALL(TARGETS ${LIBRARY_NAME}
  DESTINATION ${${LIBRARY_NAME}_INSTALL_DIR})

# --- GENERATE STUBS
IF(GENERATE_PYTHON_STUBS)
  LOAD_STUBGEN()
  GENERATE_STUBS(${CMAKE_CURRENT_BINARY_DIR} ${LIBRARY_NAME} ${ABSOLUTE_PYTHON_SITELIB})
ENDIF(GENERATE_PYTHON_STUBS)

# --- INSTALL SCRIPTS
SET(PYTHON_FILES
  __init__.py
  utils_render.py
)

FOREACH(python ${PYTHON_FILES})
  PYTHON_BUILD(${LIBRARY_NAME} ${python})
  INSTALL(FILES
    "${CMAKE_CURRENT_SOURCE_DIR}/${LIBRARY_NAME}/${python}"
    DESTINATION ${${LIBRARY_NAME}_INSTALL_DIR})
ENDFOREACH(python)
